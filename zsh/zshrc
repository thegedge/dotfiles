#----------------------------------------------------------------------
# Setting TRACING=1 will enable some tracing statements which help you
# profile slow parts of your zshrc.
#----------------------------------------------------------------------
TRACING=0
if [[ $TRACING -ne 0 ]]; then
  zmodload zsh/datetime
  setopt promptsubst
  # Set the trace prompt to include seconds, nanoseconds, script name and
  # line number This is GNU date syntax; by default Macs ship with the BSD
  # date program, which isn't compatible
  PS4='+${EPOCHREALTIME} %N:%i> '
  # Save file stderr to file descriptor 3 and redirect stderr (including
  # trace output) to a file with the script's PID as an extension.
  exec 3>&2 2>/tmp/startlog.$$
  # Set options to turn on tracing/expansion of commands contained in the
  # prompt.
  setopt xtrace prompt_subst
fi

#----------------------------------------------------------------------
# zsh options
#----------------------------------------------------------------------
# .. use a cache file
zstyle ':completion:*' use-cache on

# .. and then specify the cache file to use (not added to repo: separate file for each machine)
zstyle ':completion:*' cache-path ~/.zshcache

# More glob features
setopt EXTENDED_GLOB

# All non-alphanumeric characters are word separators
export WORDCHARS=''

DOTFILES_ZSH_DIR="$(dirname $(readlink ${HOME}/.zshrc))"
source "${DOTFILES_ZSH_DIR}/key-bindings.zsh"

#----------------------------------------------------------------------
# Prezto - config framework for ZSH
#----------------------------------------------------------------------
PREZTO_DIR="${ZDOTDIR:-$HOME}/.zprezto"
if [[ -s "${PREZTO_DIR}/init.zsh" ]]; then
  source "${PREZTO_DIR}/init.zsh"
fi

#----------------------------------------------------------------------
# Include any local configuration
#----------------------------------------------------------------------
if [[ -e "${HOME}/.zshrc.local" ]]; then
  source "${HOME}/.zshrc.local"
fi

#----------------------------------------------------------------------
# Runs all scripts in profile.d
#----------------------------------------------------------------------
for file in $(dirname "$(readlink ${HOME}/.zshrc)")/../profile/profile.d/*(on); do
  if [[ -f "${file}" ]]; then
    source ${file}
  fi
done

#----------------------------------------------------------------------
# Disable tracing
#----------------------------------------------------------------------
if [[ ${TRACING} -ne 0 ]]; then
  # Turn off tracing.
  unsetopt xtrace
  # Restore stderr to the value saved in FD 3.
  exec 2>&3 3>&-
fi
