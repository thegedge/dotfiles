#----------------------------------------------------------------------
# Setting TRACING=1 will enable some tracing statements which help you
# profile slow parts of your zshrc.
#----------------------------------------------------------------------
if [[ -n "${TRACING}" ]]; then
  zmodload zsh/datetime
  setopt promptsubst
  PROFILE_START_TIME="${EPOCHREALTIME}"
  # Set the trace prompt to include seconds, nanoseconds, script name and
  # line number This is GNU date syntax; by default Macs ship with the BSD
  # date program, which isn't compatible
  PS4='$(( 1000*(EPOCHREALTIME - PROFILE_START_TIME) )) %N:%i> '
  # Save file stderr to file descriptor 3 and redirect stderr (including
  # trace output) to a file with the script's PID as an extension.
  exec 3>&2 2>/tmp/startlog.$$
  # Set options to turn on tracing/expansion of commands contained in the
  # prompt.
  setopt xtrace prompt_subst
fi

#----------------------------------------------------------------------
: zsh options
#----------------------------------------------------------------------

# All non-alphanumeric characters are word separators
export WORDCHARS=''

#----------------------------------------------------------------------
: Prezto - config framework for ZSH
#----------------------------------------------------------------------
PREZTO_DIR="${ZDOTDIR:-$HOME}/.zprezto"
if [[ -s "${PREZTO_DIR}/init.zsh" ]]; then
  source "${PREZTO_DIR}/init.zsh"
fi

#----------------------------------------------------------------------
: Source my own stuff now
#----------------------------------------------------------------------

if [[ -e "$HOME/.profile" ]]; then
  source "$HOME/.profile"
fi

if [[ -e "${DOTFILES_DIR}/zsh/key-bindings.zsh" ]]; then
  source "${DOTFILES_DIR}/zsh/key-bindings.zsh"
fi

#----------------------------------------------------------------------
# Disable tracing
#----------------------------------------------------------------------
if [[ -n "${TRACING}" ]]; then
  # Turn off tracing.
  unsetopt xtrace
  # Restore stderr to the value saved in FD 3.
  exec 2>&3 3>&-
  # Check out the startup log
  $PAGER /tmp/startlog.$$
fi
